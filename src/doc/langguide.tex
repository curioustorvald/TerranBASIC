\quad
\chapterprecishere{``Begin at the beginning'', the King said gravely, ``and go on till you come to the end: then stop.''\par\raggedleft --- \textup{Lewis Carroll, } Alice in Wonderland}

We'll begin at the beginning; how beginning? This:

\begin{lstlisting}
10 PRINT 2+2
run
4
Ok
\end{lstlisting}

Oh \emph{boy} we just did a computation! It printed out \code{4} which is a correct answer for $2+2$ and it didn't crash!

\section[GOTO]{GOTO here and there}

\index{GOTO (tutorial)}
\code{GOTO} is used a lot in BASIC, and so does in \tbas. \code{GOTO} is a simplest method of diverging a program flow: execute only the part of the program conditionally and perform a loop.

Following program attempts to calculate a square root of the input value,  showing how \code{GOTO} can be used in such manner.

\begin{lstlisting}
10 X=1337
20 Y=0.5*X
30 Z=Y
40 Y=Y-((Y^2)-X)/(2*Y)
50 IF NOT(Z==Y) THEN GOTO 30 : REM 'NOT(Z==Y)' can be rewritten to 'Z<>Y' 
100 PRINT "Square root of ";X;" is approximately ";Y
\end{lstlisting}

Here, \code{GOTO} in line 50 is used to perform a loop, which keeps looping until \code{Z} and \code{Y} becomes equal. This is a newtonian method of approximating a square root. 

\section[Subroutine with GOSUB]{What If We Wanted to Go Back?}

\index{GOSUB (tutorial)}
But \code{GOTO} only jumps, you can't jump \emph{back}. Well, not with that attitute; you \emph{can} go back with \code{GOSUB} and \code{RETURN} statement.

This program will draw a triangle, where the actual drawing part is on line 100--160, and only get jumped into it when needed.

\begin{lstlisting}
10 GOTO 1000
100 REM subroutine to draw a segment. Size is stored to 'Q'
110 PRINT SPC(20-Q);
120 Q1=1 : REM loop counter for this subroutine
130 PRINT "*";
140 Q1=Q1+1
150 IF Q1<=Q*2-1 THEN GOTO 130
160 PRINT : RETURN : REM this line will take us back from the jump
1000 Q=1 : REM this is our loop counter
1010 GOSUB 100
1020 Q=Q+1
1030 IF Q<=20 THEN GOTO 1010
\end{lstlisting}

\section[FOR--NEXT Loop]{FOR ever loop NEXT}

\index{FOR--NEXT (tutorial)}
As we've just seen, you can make loops using \code{GOTO}s here and there, but they \emph{totally suck}, too much spaghetti crashes your cerebral cortex faster than \emph{Crash Bandicoot 2}. Fortunately, there's a better way to go about that: the FOR--NEXT loop!

\begin{lstlisting}
10 GOTO 1000
100 REM subroutine to draw a segment. Size is stored to 'Q'
110 PRINT SPC(20-Q);
120 FOR Q1=1 TO Q*2-1
130 PRINT "*";
140 NEXT : PRINT
150 RETURN
1000 FOR Q=1 TO 20
1010 GOSUB 100
1020 NEXT
\end{lstlisting}

When executed, this program print out \emph{exactly the same} triangle, but code is much more straightforward thanks to the \code{FOR} statement.

\section[Get User INPUT]{Isn't It Nice To Have a Computer That Will Question You?}

What fun is the program if it won't talk with you? You can make that happen with \code{INPUT} statement.

\begin{lstlisting}
10 PRINT "WHAT IS YOUR NAME";
20 INPUT NAME
30 PRINT "HELLO, ";NAME
\end{lstlisting}

This short program will ask your name, and then it will greet you by the name you told to the computer.

\section[Function]{Function}

\index{function (tutorial)}
While you can put some pieces of codes some corner of the entire program and \code{GOSUB} there, they're honestly bad --- and you also have to keep track of which variables are used to hold temporary values, and there are more things that are just \emph{bunch of poopy nonsense}.

Consider the following code:

\begin{lstlisting}
10 DEFUN POW2(N)=2^N
20 DEFUN DCOS(N)=COS(PI*N/180)
30 FOR X=0 TO 8
40 PRINT X,POW2(X)
50 NEXT
60 PRINT "----------------"
70 FOREACH A=0!45!90!135!180!NIL
80 PRINT A,DCOS(A)
90 NEXT
\end{lstlisting}

Here, we have defined two functions to use in the program: \code{POW2} and \code{DCOS}. Also observe that functions are defined using variable \code{N}s, but we use them with \code{X} in line 40 and with \code{A} in line 80: yes, functions can have their local name so you don't have to carefully choose which variable name to use in your subroutine.

\newcounter{curryingappearance}\setcounter{curryingappearance}{\value{page}}
Except a function can't have statements that spans two- or more BASIC lines; but there are ways to get around that, including \code{DO} statement and \emph{functional currying.}

This sample program also shows \code{FOREACH} statement, which is same as \code{FOR} but works with arrays.

\section[Recursion]{BRB: Bad Recursion BRB: Bad Recursion BRB: Bad Recursion BRB: Bad RecursionBRB: Bad Recursion BRBRangeError: Maximum call stack size exceeded}

\index{recursion (tutorial)}
But don't get over-excited, as it's super-trivial to create unintentional infinite loop:

\begin{lstlisting}
10 DEFUN FAC(N)=N*FAC(N-1)
20 FOR K=1 TO 6
30 PRINT FAC(K)
40 NEXT
\end{lstlisting}

(if you tried this and computer becomes unresponsive, hit Ctrl-C to terminate the execution)

This failed attempt is to create a function that calculates a factorial of \code{N}. It didn't work because there is no \emph{halting condition}: didn't tell computer to when to escape from the loop.

$n \times 1$ is always $n$, and $0!$ is $1$, so it would be nice to break out of the loop when \code{N} reaches $0$; here is the modified program:

\begin{lstlisting}
10 DEFUN FAC(N)=IF N==0 THEN 1 ELSE N*FAC(N-1)
20 FOR K=1 TO 10
30 PRINT FAC(K)
40 NEXT
\end{lstlisting}

Since \code{IF-THEN-ELSE} can be chained to make third or more conditions --- \code{IF-THEN-ELSE IF-THEN} or something --- we can write a recursive Fibonacci function:

\begin{lstlisting}
10 DEFUN FIB(N)=IF N==0 THEN 0 ELSE IF N==1 THEN 1 ELSE FIB(N-1)+FIB(N-2)
20 FOR K=1 TO 10
30 PRINT FIB(K);" ";
40 NEXT
\end{lstlisting}

\section[Higher-order Function]{The Functions of the High Order}

\index{higher-order function (tutorial)}
Higher-order functions are functions that either takes another function as an argument, or returns a function. This sample program shows how higher-order functions can be constructed.

\begin{lstlisting}
10 DEFUN APPLY(F,X)=F(X)
20 DEFUN FUN(X)=X^2
30 K=APPLY(FUN,42)
40 PRINT K
\end{lstlisting}

Here, \code{APPLY} takes a function \code{F} and value \code{X}, \emph{applies} a function \code{F} onto the value \code{X} and returns the value. Since \code{APPLY} takes a function, it's higher-order function.

\section[MAPping]{Map}

\index{MAP (tutorial)}
\code{MAP} is a higher-order function that takes a function (called \emph{transformation}) and an array to construct a new array that contains old array transformed with given \emph{transformation}.

Or, think about the old \code{FAC} program before: it merely printed out the value of $1!$, $2!$ \ldots\ $10!$. What if we wanted to build an array that contains such values?

\begin{lstlisting}
10 DEFUN FAC(N)=IF N==0 THEN 1 ELSE N*FAC(N-1)
20 K=MAP(FAC, 1 TO 10)
30 PRINT K
\end{lstlisting}

Here, \code{K} holds the values of $1!$, $2!$ \ldots\ $10!$. Right now we're just printing out the array, but being an array, you can make actual use of it.

\section[Closure]{The Function with No Name}

\index{closure (tutorial)}
But \code{DEFUN F(X)} is only there for partial compatibility with traditional BASICs, of which their syntax is \code{DEF FNF(X)}. \code{DEFUN} cannot define nested functions, it's not a lambda-calculus system, yaddi yadda.

No, we want to be up-to-date; we don't always want every function to be global; we want to utter \emph{give me a closure, bar-tender}.

\tbas\ presents you: a closure. What does it do?

\begin{lstlisting}
10 FAC=[N]~>IF N==0 THEN 1 ELSE N*FAC(N-1)
20 K=MAP(FAC, 1 TO 10)
30 PRINT K
\end{lstlisting}

Here, \code{[N]\basicclosure\,\ldots} defines a \emph{closure} (anonymous function) that has single parameter \code{N}, then assigns it to global variable \code{FAC}.

But \emph{stop right there, criminal scum}: in what way is that an \emph{anonymous function}?

Ah-ha, take a look at this:

\begin{lstlisting}
10 F=[X]~>MAP([X]~>[X]<=5,X)
20 PRINT F(1 TO 10)
\end{lstlisting}

Here, \code{MAP} inside of the global function \code{F} has an internal function: \code{[X]\basicclosure[X]<=5}

This function is anonymous: only the \code{MAP} can use it and is not accessible from the other scopes. Even if the \code{F} and the anonymous function use same parameter name of \code{X}, they don't matter because two functions are different.

\section{Monad}

And obviously it's time to talk about the Monad. What is it? Well, you can say \emph{a monad is just a monoid in the category of endofunctors}\footnote{Saunders Mac Lane, \emph{Categories for the Working Mathematician}}, but it's not very helpful.

Monad can be seen as a container that holds whatever the value it can accept, and allows alteration of the value by \emph{binding}, and its internals can be evaluated later.

\ldots Pretty vague, eh? But thanks to its broad definition, it can be used to implement many things. For example, let me show you how monad can be used to add memoisation (and thus making it faster!) to the forementioned Fibonacci sequence generator, without clobbering a global variable, of course:

\begin{lstlisting}
10 FIB_=[N,M]~>IF LEN(MJOIN(M))>=N THEN HEAD(MJOIN(M)) ELSE FIB_(N,M>>=([XS]~>MRET((XS(0)+XS(1))!XS)))
11 FIB=[N]~>FIB_(N,MRET(1!1!NIL))
20 FOR K=1 TO 10
30 PRINT FIB(K);" ";
40 NEXT
\end{lstlisting}

One more function \code{FIB\_} had to be used,\footnote{It can be done without one by using \emph{undefined}, but then the code gets needlessly complex for a tutorial.} but the fibonacci sequence generator now runs much faster because the monad now holds the results from previous runs, making double-recursion run unnecessary unlike the previous fibonacci generator.

And this is exactly what \emph{memoisation} is, remembering (or \emph{memo}ing) the previous results.

\section[Currying]{Haskell Curry Wants to Know Your Location}
\label{currying101}

\vspace*{-\mytextsize}\ \par % a dummy paragraph to let the counter increment because motherfscking latex
\index{curry (tutorial)}
Just \newcounter{curryingselfref}\setcounter{curryingselfref}{\value{page} - \value{curryingappearance}}\cnttoenglish{\value{curryingselfref}}{page} ago there was a mentioning about something called \emph{functional currying}. So what the fsck is currying? Consider the following code:

\begin{lstlisting}
10 DEFUN F(K,T)=ABS(T)==K
20 CF=F~<32
30 PRINT CF(24) : REM will print 'false'
40 PRINT CF(-32) : REM will print 'true'
\end{lstlisting}

Here, \code{CF} is a curried function of \code{F}; built-in operator \code{\basiccurry} applies \code{32} to the first parameter of the function \code{F}, which dynamically returns a \emph{function} of \code{CF(T) = ABS(T) == 32}. The fact that Curry Operator returns a \emph{function} opens many possibilities, for example, you can create loads of sibling functions without making loads of duplicate codes.

But, what if we pre-cook the curry before serve? The \code{\basiccurry} operator is there to curry an un-curried function; we wouldn't really need that if the function was curried in the begin with.

Here, \emph{closure} do wonders as well; for a fun of it, let's re-write the \code{F(K,T)} to be pre-curried:

\begin{lstlisting}
10 F=[K]~>[T]~>ABS(T)==K
20 CF=F(32)
30 PRINT CF(24) : REM will print 'false'
40 PRINT CF(-32) : REM will print 'true'
\end{lstlisting}

The function \code{F}, when called, returns its inner function \code{[T]\basicclosure ABS(T)==K} with \code{K} being substituted with the argument that were applied to \code{F}, so the function \code{CF} here can be expressed as: \code{[T]\basicclosure(T)==32}

The subsequent calls for \code{CF} returns appropriate values, in the same manner as the descriptions above.

\section[Wrapping-Up]{The Grand Unification}

Using all the knowledge we have learned, it should be trivial\footnote{/s} to write a Quicksort function in \tbas, like this:

\begin{lstlisting}
10 QSORT=[XS]~>IF LEN(XS)<1 THEN NIL ELSE 
    QSORT(FILTER([X]~>X<HEAD XS,TAIL XS)) # HEAD(XS)!NIL # 
    QSORT(FILTER([X]~>X>=HEAD XS,TAIL XS))
100 L=7!9!4!5!2!3!1!8!6!NIL
110 PRINT L
120 PRINT QSORT(L)
\end{lstlisting}

Line 10 implements quicksort algorithm. \code{QSORT} selects a pivot by taking the head-element of the array \code{XS}\footnote{Stands for \emph{X's}.} with \code{HEAD XS}, then utilises anonymous functions \code{[X]\basicclosure X < HEAD XS} and \code{[X]\basicclosure X >= HEAD XS} to move lesser-than-pivot values to the left and greater to the right (the head element itself does not get recursed, here \code{TAIL XS} is applied to make head-less copy of the array), and these two separated \emph{chunks} are recursively sorted using the same \code{QSORT} function. Closure is exploited to define a comparison functions. \code{HEAD(XS)!NIL} creates a single-element array contains head-element of the \code{XS}.
